plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
    id 'org.groovymc.modsdotgroovy' version '1.4.1'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

mixin {
    add sourceSets.main, 'curlsedtooltips.refmap.json'
    config 'curlsedtooltips.mixins.json'
    debug.export = true
}

group = 'com.matyrobbrt.curlsedtooltips'
archivesBaseName = 'curlsedtooltips'
modsDotGroovy.dslVersion = '1.5.0'; modsDotGroovy.platform 'forge'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: 'parchment', version: '2023.06.26-1.20.1'

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        all {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'curlsedtooltips'
            mods {
                curlsedtooltips {
                    source sourceSets.main

                }
            }
        }
        
        client {}
        server {}
        gameTestServer {}

        data {
            workingDirectory project.file('run')
            args '--mod', 'curlsedtooltips', '--all',
                    '--output', file('src/generated/resources/'),
                    '--existing', file('src/main/resources/') 
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft 'net.minecraftforge:forge:1.20.1-47.0.34'
annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

final manifestAttributes = [
        'Specification-Title'   : 'curlsedtooltips',
        'Specification-Vendor'  : 'Matyrobbrt',
        'Specification-Version' : '1',
        'Implementation-Title'  : project.name,
        'Implementation-Version': project.jar.archiveVersion,
        'Implementation-Vendor' : 'Matyrobbrt'
]
manifestAttributes.put('MixinConfigs', 'curlsedtooltips.mixins.json')

jar {
    manifest.attributes(manifestAttributes)
    finalizedBy('reobfJar')
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

java {
    withSourcesJar()
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifacts = [jar, sourcesJar]
            artifactId = 'curlsedtooltips'
            version = project.minecraft_version + '-' + project.minecraft_version
        }
    }
    repositories {
        maven {
            name = 'MatyrobbrtMavenRepo'
            url = 'https://maven.matyrobbrt.com/releases'
            credentials {
                username = findProperty('matyrobbrtMavenUser') ?: ''
                password = findProperty('matyrobbrtMavenPassword') ?: ''
            }
        }
    }
}
